

apply plugin: 'com.android.application'
apply plugin: 'maven'
// markdown in javadoc
// https://github.com/Abnaxos/pegdown-doclet
apply plugin: 'ch.raffael.pegdown-doclet'


android {
    compileSdkVersion = 29

    defaultConfig {
        // fdroid-release 'de.k3b.android.androFotoFinder'
        // main-develop-branch 'de.k3b.android.androFotoFinder.dev'
        // applicationId 'de.k3b.android.androFotoFinder'
        applicationId 'de.k3b.android.androFotoFinder.dev'

        minSdkVersion 14		// Android 4.0 Ice Cream Sandwich (API 14); Android 4.4 KitKat (API 19);  Android 5.0 Lollipop (API 21); 
								// Android 6.0 Marshmallow (API 23); Android 7.0 Nougat (API 24)
        // maxSdkVersion 28        // #155: android-10=api29

        /* from https://developer.android.com/training/data-storage/shared/media
            If your app targets Android 10 (API level 29) or higher, in order for your app
            to retrieve unredacted Exif metadata from photos, you need to declare the ACCESS_MEDIA_LOCATION
            permission in your app's manifest, then request this permission at runtime.
        * */
        targetSdkVersion 23  // Android 6.0 Marshmallow (API 23) below compileSdkVersion to enamble more backward-compatibility

		// Releases on f-droid
        // unfortunately fdroid needs magic numbers to detect version infos
		// published 0.5.1.160629 (17)
		// published 0.5.1.160712 (18)
		// published 0.5.2.160726 (19)
        // not published 0.5.2.160920 (20)
        // 0.5.3.160927 (21)    mapsforge offline maps; *.gpx and *.kml files
        // 0.5.4.161111 (22)
        // 0.5.4.161112 (23)
        // 0.5.5.161220 (24)    Image Picker
        // 0.6.0.170309 (25)    Tag support (fdroid build failed)
        // 0.6.0.170315 (26)    Bugfix for new Tag support (fdroid build failed)
        // 0.6.0.170402 (27)    language updates. (fdroid build failed)
        // 0.6.0.170404 (28)    same as 0.6.0.170402. try to fix fdroid build
        // 0.6.0.170421 (29)    bugfix Map
        // 0.6.0.170502 (30)    workaround "android stock icon as activity icon breaks aapt check in frdoid build"
        // 0.6.1.170803 (31)    edit exif; enhanced media scanner
        // 0.6.1.170816 (32)    Bugfix crash android-7.1
        // 0.6.2.171114 (33)    Bugfix: non-root android-5 (or newer) direcotry picker not working because of permissions. Necessary for copy/move file commands.
        // 0.6.2.171126 (34)    Bugfix Crash (NullPointerException) in mediascanner after updating exif
        // 0.6.3.180211 (35)    Extended Applocking; Autoprocessingmode for copy/move; Private images
        // 0.6.4.180314 (36)    Buggy: Fix/Improved Autoprocessingmode, Menu "open in filemanager" & "rename folder"-Crash in Settings
        // 0.6.4.180321 (37)    Bugfix for 0.6.4.180314 (36)
        // 0.6.9.180813 (37)    public betta: Searchbar, vitual-folder, new icons
        // 0.7.0.180823 (38)    Searchbar, vitual-folder, new icons
        // 0.7.1.180830 (39)    Bugfix for 38; translation uk
        // 0.7.2.181027 (40)    Bugfixes: image orientation, media scanner
        // 0.7.3.190424 (41)    mapsforge-v5, filter date-last-modified, Fix getContent, exif-TAG_DATETIME, Translation ja, nl, ru, pt-br, uk
        // 0.7.4.190801 (42)    hotfix to reallow openstreetmap online map download
        // 0.7.5.190916 (43)    Drill Down Search via Picker context-show-in-new
        // 0.8.0.191021 (44)    Backup to zip
        // 0.8.1.200212 (45)    Theme Support
        // 0.8.1.200218 (46)    Bugfix Delete Image
        // 0.8.3.200315 (47)    Bugfix Backup to zip
        // 0.8.4.210419 (48)    SD-Card support via SAF

        versionCode = 48
        versionName = '0.8.4.210419'

		// all supported locales
		// resConfigs "ar","de","es","fr","hi","in","it","ja","nl","pl","ro","ru","tr","uk","zz","pt-rBR","zh-rCN","zh-rTW"
    }
	


	// does not remove german translations :-(
    // sourceSets.main.res.filter.exclude '**/res/values-de/**'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        debug {
			// shrinkResources true
            // minifyEnabled true
			minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
			shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
	
	lintOptions {
		// http://stackoverflow.com/questions/31350350/generating-signed-apk-error7-missingtranslation-in-build-generated-res-gen
		// MissingTranslation : not all crowdwin translations are complete so ignore them
		// ValidFragment : local (dialog-)fragment class for customized direcotry picker is fragile but on rotation code makes shure that dialog is closed. 
        // disable 'MissingTranslation','ValidFragment', 'ContentDescription', 'AndroidLintPluralsCandidate', 'AndroidLintRtlHardcoded'
        disable 'MissingTranslation','ValidFragment', 'ContentDescription', 'ExpiredTargetSdkVersion' 	

        abortOnError false
    }
}

// task from https://stackoverflow.com/questions/45848521/how-to-sync-android-project-by-gradle-task/45856729#45856729
import java.util.regex.Pattern
task('increaseVersionCode').doLast({
    def buildFile = file("build.gradle")
    def pattern = Pattern.compile("versionCode\\s+(\\d+)")
    def manifestText = buildFile.getText()
    def matcher = pattern.matcher(manifestText)
    matcher.find()
    def versionCode = Integer.parseInt(matcher.group(1))
    def manifestContent = matcher.replaceAll("versionCode " + ++versionCode)
    buildFile.write(manifestContent)
})

dependencies {

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.slf4j:slf4j-api:1.7.7@jar'
    testImplementation 'org.slf4j:slf4j-simple:1.7.7'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':fotolib2')
    implementation project(':libK3bFilefacade')
    // implementation project(':k3b-geoHelper')
    implementation 'com.github.k3b:k3b-geoHelper:1.1.5'

    // https://github.com/nostra13/Android-Universal-Image-Loader
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'

    // zoomable image view
    // from https://github.com/chrisbanes/PhotoView
    // implementation 'com.github.chrisbanes.photoview:library:1.2.3'
    // implementation project(':fotoview.library')  // local copy of PhotoView
    // implementation 'com.github.k3b:PhotoView:v1.2.6.k3b-7-SNAPSHOT' { transitive = false } // via jitpack.io last bugfix on 2018-10-17
    implementation project(':photoview')
    // for view-pager
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    // implementation 'com.android.support:support-v4:28.0.0' not supported by travix yet
    // implementation 'com.android.support:support-v4:23.1.1'
    // implementation 'com.android.support:support-v4:21.0.2'
    // implementation 'com.android.support:appcompat-v7:21.0.2'
    // exif support
    // https://github.com/drewnoakes/metadata-extractor/wiki/GettingStarted licence: http://www.apache.org/licenses/LICENSE-2.0
    // 2.10.1 includes more recent xmpcore that generates java-8 bytecode not supported by android yet
    implementation ('com.drewnoakes:metadata-extractor:2.15.0') { transitive = false } // 2.8.1
	
	// implementation 'com.adobe.xmp:xmpcore:6.1.10' // update for drewnoakes:metadata-extractor requires java-8 compiler does not run on my android-4.4.
	implementation 'com.adobe.xmp:xmpcore:6.1.11'
    // current version for drewnoakes:metadata-extractor

    // implementation 'com.github.k3b:slideDateTimePicker:v1.0.5' // via jitpack.io

    // implementation 'org.apache.commons:commons-configuration2:2.1.1'

    // this is osmdroid
    // https://github.com/osmdroid/osmdroid/  6.0.3 from 11/2018
    // https://github.com/osmdroid/osmdroid/  6.1.0 from 03/2019
    implementation 'org.osmdroid:osmdroid-android:6.1.10'
    implementation ('org.osmdroid:osmdroid-mapsforge:6.1.10')


    //Mapsforge rendering and database support, which is LGPL
    implementation 'org.mapsforge:mapsforge-map-android:0.15.0'
    implementation 'org.mapsforge:mapsforge-map:0.15.0'
    implementation 'org.mapsforge:mapsforge-themes:0.15.0'

    implementation 'org.slf4j:slf4j-api:1.7.7@jar'
    implementation 'eu.lp0.slf4j:slf4j-android:1.7.7-1@jar'


    // https://jitpack.io/com/github/k3b/ToGoZip/L2.0.15/build.log
    // https://jitpack.io/com/github/k3b/ToGoZip/libK3bZip/L2.0.15/
    // implementation('com.github.k3b.ToGoZip:libK3bZip:L2.0.15')  { transitive = false; }
    implementation('com.github.k3b.ToGoZip:libK3bZip:v2.1.27.210419')  { transitive = false }
    implementation('com.github.k3b.ToGoZip:libK3bAndroidZip:v2.1.27.210419')  { transitive = false }

    testImplementation('org.powermock:powermock-api-mockito:1.6.6')
    testImplementation('org.powermock:powermock-module-junit4:2.0.9')

}

